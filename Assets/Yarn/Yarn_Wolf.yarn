title: Yarn_Wolf
tags:
---
// First time talking to Wolf
<<if $wolfFirstChat is true>>
    <<set $wolfFirstChat to false>>

    // --- Dialogue for first chat with Wolf here
    Lou: <i>Awoooo...</i>
    Lou: <i>Awooooooo....</i>
    <i>The wolf jumps, noticing that they have an acquired an audience to their soft whimpers.</i>
    Lou: O-oh, h-hello...
    Lou: A-are you here to buy some produce? 
    Lou: Some... Some <i>tomatoes</i> maybe??
        -> I'd love to buy some tomatoes!
            Lou: ...
        -> Are you alright?
            Lou: ...
    Lou: <i>*sniffling*</i>
    Lou: ...I'm so sorry, I can't--
    Lou: I CAN'T HARVEST ANYTHING RIGHT NOW-OOOOOOOOOOO
    Lou: I KEEP CRUSHING THE TOMATOES EVERY TIME I TRY AWOOOOO
    Lou: I-if only I wasn't out of grandpa's special <b>Wolf-B-Gone salve</b>, I'd be able control my grip strength--
    Lou: P-please, if you insist on buying tomatoes, could you come back when I've made some more salve?
    Lou: I-it won't take too long--there's some wolfsbane in a little clearing in the woods…just down the road to the West…
    Lou: ...Oh, but the woods are s-so creepy and dark... 
    Lou: Ohhhh....
    Lou: ...
    Lou: Just a few more minutes.
    Lou: T-then I'll go.


// Subsequent chats with Wolf, haven't given salve yet
<<elseif $gaveKeyItem3 is false>>

    // --- Any dialogue Wolf should say before a check for the Salve happens
    Lou: What was the recipe again?
    Lou: <i>Oat and chamomile to soothe...</i>
    Lou: <i>Honeyed wax to salve and smooth...</i>
    Lou: <i>Tame thine wolf with purple bane...</i>
    Lou: <i>...To sleep till moon comes round again.</i>
    Lou: <sub>But I don't want to enter the woods by myself.</sub>
    Lou: <sub>Maybe if I wait long enough, this customer will give up on me and leave? </sub>

    // --- Dialogue if interacted a third time but no salve yet
    //Lou: Oats. Chamomile. Wax. Wolfsbane.
    //Lou: But I don't want to enter the woods by myself.
    //Lou: Maybe if I wait long enough, this customer will give up on me and leave?


    <<if $craftedKeyItem3 is true>>     // Salve
        <<set $matItem6 to 1>>          // Wolf gives player 1 tomato
        <<set $gaveKeyItem3 to true>>   // will no longer repeat themselves

        // --- Dialogue for giving Wolf salve here
        Lou: <i>The countryside would be fun, they said. You should take a break from being in town, they said...</i>
        Lou: O-oh! I didn't see you there. S-sorry.
        Lou: D-did you still want tomatoes after all? 
        Lou: Only... Only... I haven't--
        Lou: <i>Awooooo.... </i>
            -> I made you some Wolf-B-Gone!
                Lou: Oh...
                Lou: Oh??? You made more? How did you know the recipe?
                Lou: Did you know my grandpa too? Everyone here knows him apparently--
            -> Yep, still here for the tomatoes.
                Lou: Oh...
                Lou: I'm s-sorry, but I'm not even a real farmer--my grandpa left me this farm--
                Lou: Wait. Is that... is that--<i>ACHOO!</i>--Wolf-B-Gone I smell???
                Lou: <i>Wolfsbane always tickles my nose like nothing else--</i>
                Lou: How did you-- Did you brave the really scary forest--?!
            
        Lou: S-sorry, I'm just so excited to be able to hold a tomato without crushing it again.
        The wolf aggressively slathers the purple balm all over the furry forearms without a second thought.
        Lou: YES!!! I can feel it working! 
        Lou: AWOOOOOOOOHOOHOOHAHAHA!
        Lou: I can't stop crying--I'm so--I can do it grandpa! I can harvest my first tomato crop!
        Lou: I won't have to go back to being a soulless henchman for an evil liege lord who mistreats his serfs! I <i>can</i> be my own boss!
        Lou: Here, take some tomatoes, on the house--I insist!
        


    <<endif>>

// Subsequent chats with Wolf here, salve given
<<else>>

    // --- Dialogue for after giving Wolf Salve here
    Lou: Tell all your friends about this farm! Once they have wolf-grown wolf peaches, they'll never go back, I promise.
    Lou: <sub>And then I'll use the money to buy more seeds and use those seeds to grow more crops and as long as I time it right and track everything in meticulously kept charts--</sub>
    Lou: Well. <i>Maybe</i> I'll also have to go into the woods again to get more wolfsbane, b-but--
    Lou: W-we don't have to worry about that right now!!! I-I'm being a real adult already!!
    Lou: <i><sub>...Awoooo... </sub></i>

<<endif>>

===
