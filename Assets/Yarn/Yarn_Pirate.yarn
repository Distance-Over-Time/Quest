title: Yarn_Pirate
tags:
---
// First time talking to Pirate
<<if $pirateFirstChat is true>>
    <<set $pirateFirstChat to false>>

    // --- Dialogue for first chat with Pirate here
    Dubloon: <i>22...23...24...</i>
    Dubloon: <i>25...26...27 golden beauties--</i>
    <i>The skeletal sailor SLAMS the chest he's hunched over shut.</i>
    Dubloon: ARRR! AVAST!
    Dubloon: Stay back or I'll gut ye from head to toe!
    Dubloon: I know what ye be here fer an' ye won't get it! Over my undead body!
    Dubloon: ...
    Dubloon: Which be to say ne'er!
    Dubloon: I'll drag this here chest with me to the watery depths 'fore I hand it to the likes o' you!
        -> What's in the chest?
            Dubloon: Aha! I knew ye was a thieving bilge rat!
            Dubloon: <i>Wouldn't ye like to know?</i>
            Dubloon: The knowledge won't get yer grubby hands any closer to me sour gold.
        -> I'm not here for the chest.
            Dubloon: Aha! I knew it--!
            Dubloon: ...Y-yer not?
            Dubloon: L-lies! Everyone be here fer me treasure!!
            Dubloon: ...
            Dubloon: ...Ye really aren't interested in the sour gold?

    Dubloon: ...I mean that there be no such thing inside this here chest. S-so don't think ye'll be gettin' any.
    Dubloon: Even if ye asked nicely.
    Dubloon: <sub> Though I wouldn't be opposed to hearin' that, maybe. No one e'er asks me anythin' nice-like. Not since the scurvy.</sub>
    Dubloon: ARR! I mean, I won't part with a single o' these golden beauties for anything less than an equivalent weight in gold. Or grog.
    Dubloon: Or <b>golden grog</b>. I be hearing tales of some fancy magic spirits from 'round these here parts. 
    Dubloon: Ye want even a glimpse o' me treasures, ye'll have to pay up. And don't be offering me no plain watered down mead. I can still taste the difference!
    

// Subsequent chats with Pirate, haven't given Grog yet
<<elseif $gaveKeyItem1 is false>>
    Dubloon: 'Bout those tales I be hearing... 'bout a famous golden drink... 
    Dubloon: They say dead men don't tell 'em, but...
    Dubloon: ...I--I could be persuaded to share some with ye, if ye asked.
    Dubloon: The tales! Not the grog!
    Dubloon: ...
    Dubloon: <sub>Are ye bones be gettin' soft?! What scourge of the seven seas says somethin' like that--</sub>
    Dubloon: Er-- Not the tales! Or the grog! 
    Dubloon: Get out of here ye scallywag 'fore I do somethin' unforgiveable like-- like askin' ye to stay for a meal or somethin'!
    Dubloon: I-I mean, step to! Away with you! 

    // --- Any dialogue Pirate should say before a check for the Grog happens
    Dubloon: Belay yer carousin'--Ye think ye can hornswaggle Captain Dubloon? 
    Dubloon: I've sailed with enough cheap cooks and drunks and cheap, drunk cooks to know watered mead on sight, ye daft knave.
    Dubloon: The magic grog <i>sparkles</i>. Made by some poncy fairies or somethin'.
    Dubloon: It's yer lucky day, matey. I've become rrrreal patient since I, uh, took a swim and lost a lot of weight...
    Dubloon: If ye hurry back, I might even be persuaded to tell ye 'bout that whole cursed venture.
    Dubloon: <sub>What are ye even sayin', Dubloon?!</sub>
    Dubloon: O-or I'll cut ye down where ye be standing, ye hear?!

    <<if $craftedKeyItem1 is true>>     // Grog
        <<set $matItem7 to 1>>          // Pirate gives player 1 lemon
        <<set $gaveKeyItem1 to true>>   // will no longer repeat themselves

        // --- Dialogue for giving Pirate Grog here
        Dubloon: Yer back!
        Dubloon: Of course, I wasn't watchin' fer yer return or anythin' o' the sort.
        Dubloon: My lips be parched is all. With all this talk of fey spirits.
            -> One bottle of golden grog, made just for you, matey.
                Dubloon: Ye...ye made this yerself?
                Dubloon: Fer...fer me?
                Dubloon: I mean, o' course ye did! That impressed by me fearsome reputation, eh?! Captain Dubloon who leaves no quarter!
                Dubloon: That an' I'm sure ye didn't want to be run through with me cutlass.
            -> Would you please accept this trade of golden grog for sour gold?
                Dubloon: W-well when ye says it that way, what fine gentleman of fortune such as meself could rightly refuse such a trade?
                Dubloon: I suppose I be willing to part with a single piece o' my treasure since ye paid the price I named.
        
        Dubloon: But first, a taste. 
        <i>The pirate uncorks the bottle with his teeth, downing the golden fluid in swift gulps. It is unclear where the liquid is going.</i>
        Dubloon: ...
        Dubloon: ...
        Dubloon: Aye, that sure be the taste of a fairy's touch. Runs right through ye.
        Dubloon: ...
        Dubloon: You know what every good grog needs? A healthy dose of lemon.
        Dubloon: ...Though I suppose, once I pull them out, you'll be off then.
        Dubloon: <sub>Ask them to stay a while, ye yellow-bellied chowder-head--Nay, that'd be <i>unusual</i>. Shiver me timbers, imagine saying all this aloud--</sub>
        Dubloon: <sub>I...I didn't say that out loud, right? How strong be this spirit--?</sub>
        Dubloon: ...
        Dubloon: Er, here ye are. My finest treasure is yours.
        Dubloon: An' well...if...if'n ye should find yerself in need of more citrons...
        Dubloon: Well I don't see meself leavin' this port anytime soon.

    <<endif>>

// Subsequent chats with Pirate here, Grog given
<<else>>

    // --- Dialogue for after giving Pirate Grog here
    Dubloon: ...and if ye must sell your soul to the work, at least make sure there be good plunder for it. If not, best hop ships at the next port--
    Dubloon: Hold that thought, matey.
    Dubloon: Ahoy there! I was just tellin' me new friend--
    Dubloon: Er...that is, were there always two o' ye about?
    Dubloon: ...I might be three sheets to the wind. Loaded to the gunwall as it were.
    Dubloon: It's good to see you again, bucko. Er...again, again. 
    Dubloon: Or maybe this be the second time.
    Dubloon: I've lost count.
    Dubloon: The less countin' the better, me thinks. No need to keep inventory when tradin' lemons 'tween friends.

<<endif>>

===
