title: Yarn_Elves
tags:
---
// First time talking to Elves
<<if $elvesFirstChat is true>>
    <<set $elvesFirstChat to false>>

        Oak: <sub>A customer.</sub>
        Ash: <sub>A CUSTOMER.</sub>
        <i>The two woodsprites offer deep, identical bows in perfect unison.</i>
        Oak: Welcome paying patron to our humble establishment~
        Oak: We invite you to peruse our menu of spirits, nightcaps, and libations, all made by fairy hands... 
        Ash: HOW CANS WE SERVES YOU TODAY?
        Oak: Perhaps a thimble of morning dew?
        Ash: JUICY, DELICIOUS BLACKBERRY CORDIAL?
        Oak: Flax into Gold perhaps or an elfshot of--
        Ash: <b>HONEYWINE.</b>
        Oak: Ash, my good fellow, we are out of honeywine.
        Ash: THE BEES.
        Oak: Yes, they do not slumber yet. We must pay the great dragon a visit.

        //Checking to see if player gave tea to Dragon
        <<if $gaveKeyItem0 is true>>
            Oak: Although, the trees rustle with whispers--I believe we will have honey again for our stores soon.
            Ash: YES. YESSSSS.
        <<endif>>

        Oak: So we ask again, visitor from afar, how might we two quench your thirst?
    //Checking to see if player has already made chamomile tea            
            <<if $craftedKeyItem0 is false>>
                -> I don't suppose you have chamomile tea?
                Ash: NO.
                Ash: WE TRY AND TRY.
                Ash: TEA IS TOO STRONG.
                Oak: Much too strong. You may slumber for a hundred years after a single sip.
                //checking to see if player has already found chamomile
                <<if $foundMatItem1 is false>>  
                Oak: But if it is chamomile you seek, the trees speak of a meadow overlooking the sea...
                <<else>>
                Oak: It is better brewed with hands unburdened by fey magic.
                <<endif>>
                Oak: Was there anything else we could satisfy you with in the meantime?
                    <<if $pirateFirstChat is true>>
                        <<jump gottaGo>>
                    <<endif>>
        // Checking to see if player has already spoken with Pirate
        // --- Dialogue for first chat with Elves here (talked to Pirate first)
            <<elseif $pirateFirstChat is false>>   
                <<jump grogChoice>>
            <<else>>
                    <<jump gottaGo>>
            <<endif>>
<<else>>
    <<jump elvesPart2>>

<<endif>>
        === 

        title: grogChoice
        ---                        
        -> I'm looking for some golden grog.
            Ash: GROG?! NASTY.
            Oak: It is an affront to our skills.
            Oak: Although, my dearest Ash, we do not turn away customers without taste.
            Ash: <sub>BLECH.</sub>
            Oak: Perhaps if we let them taste the golden ichor, unfiltered...untainted...
            Ash: THEY WILL LEARN! YES!
            Oak: Indeed. We will elevate your palette valued patron.
            Oak: Though, we do not do any work for free.
                <<jump porridgeChat>>
        -> I've been told of a golden drink, said to be the finest in this land...?
            Ash: PRETTY WORDS.
            Oak: Though not an exaggeration. Our honeywine indeed draws many souls to these woods.
            Oak: If this is the libation you seek, we are willing to help you taste its depths.
            Oak: Though, I must emphasize up front that we do not do any work for free.
                <<jump porridgeChat>>
        ===

        title: porridgeChat
        ---
            Ash: HNRGH. THE GOLD. I DO NOT SMELL IT.
            Oak: Nor have I heard its clink on your person.
            Oak: But your hands speak of familiarity with creation, dear patron.
            Oak: And we do love a good barter. Might we offer a trade between fellow artisans?
            Ash: TRADE! TRADE! TRADE!
            Oak: ...Are you familiar with a warm meal common to menfolk--a mix of oats and water and a dash of something sweet?
            Ash: WARM. SWEET.
            Oak: When we used to complete work in town as naive woodsprites, those grateful for our anonymous service would leave a bowl of oatmeal on the windowsill overnight.
            Oak: I believe I speak for both of us when I say we should like to taste it again.
            Ash: AGAIN AGAIN. HOT GRAIN, HOT GRAIN!
            Oak: In return, we will offer you the means and ways to make your...golden drink.
            Oak: Though you will need honey and water, at the very least, to complete the recipe we offer.
            Ash: I LOVE PORRIDGE.
            Oak: Indeed. We eagerly await your return.
            <<set $porridgeFirstChat to false>>
        ===
 
        title: gottaGo
        ---
        // --- Dialogue for first chat with Elves here (did not talk to Pirate first)
            -> I'm sorry, I can't stop for drinks.
                Ash: BORING! WORK IS BORING.
                Oak: Ash means that there is always time for merriment.
                Ash: AND DRINKS! AS LONG AS YOU HAVE COIN.
                Oak: Or a good barter. We cannot resist a binding trade...
                Oak: But a discussion for another time.
                Ash: YOU WILL BE BACK.
            -> Maybe I can return when I'm done with my errands?
                Ash: YOU WORK FOR NO COIN. FOOLISH.
                Oak: Ash means that we hope you are not being exploited. We have, ourselves, left unpaid servitude behind and embrace only payment in full.
                Oak: Though, good Ash, I believe our esteemed guest is an <i>apprentice</i>.
                Ash: YOU ARE LEARNING? SOON YOU WILL BE IN CHARGE?
                Oak: A discussion for another time perhaps, when you are free to partake.
                Ash: WAITING. WE WAITS FOR YOU.
                <<stop>>
        ===


title: elvesPart2
---    
// Subsequent chats with Elves, haven't given Porridge yet
<<if $gaveKeyItem2 is false>>

    // --- Player still hasn't talked to Pirate, still hasn't made Porridge
    <<if $pirateFirstChat is true>>

        // --- Dialogue if Player as not yet talked to Pirate, no Porridge here
        <i> Perhaps it's best to return when you have free time. Or a reason to be here. Whichever unlikely one comes first.</i>

    <<elseif $pirateFirstChat is false>>

        // --- Dialogue if Player has talked to Pirate, but has not heard about porridge yet
        <<if $porridgeFirstChat is true>>
        Oak: Esteemed patron, you return.
        Ash: WE KNEW. WE KNEW. WE SENSED IT. 
        Oak: A whisper on the wind.
        Ash: DRINK NOW? DRINK?
        <<jump grogChoice>>

        //---Dialogue if Player has talked to Pirate, but has not made porridge yet despite knowing of it
        <<else>>
        Oak: Alas, we cannot help you with the making of that golden drink until we receive payment.
        Ash: WE DO NOT CHEAT.
        Oak: Quite right, we would not cross you that way. It would be against our professional pride.
        Ash: RETURN WITH THE WARM, THE SWEET, THE OATSSSSS.
        Oak: <i>Then</i> we shall aid you as is contracted.
        <<endif>>
    <<endif>>
    // --- Any dialogue Elves should say before a check for the Porridge happens


    <<if $craftedKeyItem2 is true>>     // Porridge
        <<set $gaveKeyItem2 to true>>   // will no longer repeat themselves

        // --- Dialogue for giving Elves Porridge here

        Ash: BACK AGAIN--
        Ash: I SMELL...I SMELL--!!!
        -> I made porridge!
            Ash: PORRIDGE!!!
            Oak: Even I can barely contain my excitement. Oh, it has been an age since I tasted the joy of a bowl of sweetened oats...
        -> One bowl of hot oatmeal as ordered.
            Oak: Made exactly to order and then some, how kind of you. How very, very kind--
            Ash: IT IS TIME! 
        <i>Oak and Ash shovel heaping spoonfuls of oatmeal into their mouths, gobbling with abandon.</i>
        Ash: YES! YESSSSS!!! DELICIOUS!!!!
        Oak: Mmm~
        Oak: Our hands, skilled as they are, cannot recreate this nostalgic treat.
        Ash: THE TASTIEST!
        Oak: A-a moment please while we savor the taste--
        Ash: *oatmeal noises*
        <i> The bowl is scraped clean and then licked even cleaner.</i>
        Oak: Perfection.
        Oak: And now, as promised...
        Oak: The recipe for our famed honeywine is thus...
        //player should at this point have water and honey no matter what -- cannot make porridge without honey, honey cannot be got without water
        Ash: HONEY.
        Oak: And water.
        Oak: ...Yes, I believe that's it.
        Ash: THEN STIR. STIR! STIRRRR!!!
        <i>The apprentice tentatively pours the water and honey into a waiting bottle.</i>
        <i>They stir the contents, honey swirling into water, blending, blending until...</i>
        Ash: CORK.
        Ash: AND WAIT.
        Oak: Yes, now we wait.
        Ash: ...
        Oak: ...
        Ash: ...
        Oak: ...
        Ash: ...
        Oak: ...
        -> ...How long do we wait?
            Oak: ...It <i>does</i> seem to require a long time in human years, doesn't it?
        -> Is it...ready?
            Ash: NO!
            Ash: TAKING LONGER THAN EXPECTED.

        Oak:...Perhaps...
        Oak: You <i>did</i> mix it yourself, so we fulfilled our end of the bargain to teach you the means and methods...
        Oak: But to ensure you walk out of here, palate elevated...
        Oak: Surely...
        Ash: NO ONE WILL KNOW.
        <i>Ash and Oak grin at each other, all teeth. Sharp teeth.</i>
        <i>PING! They tap on the bottle in unison. The apprentice blinks.</i>
        <i>What was once just globs of honey haphazardly mixed into water becomes an amber nectar, somehow both viscous and transparent, glinting in the light.</i> 
            <<set $craftedKeyItem1 to true>>          // Elves gives player 1 Grog (?)
        Oak: There you are, one honeywine. I mean, er--
        Ash: DISGUSTING GROG.
        Oak: No, no, Ash, I'm sure it'll be delicious. Like no grog our friend here has ever tasted. I would swear it on the boughs and the leaves.
        Ash: DELICIOUS <i>*gag*</i> GROG.
        Oak: Enjoy your drink then, and come back again soon when you have a craving for more!
        Ash: YOU WILL CRAVE MORE!

    <<endif>>

// Subsequent chats with Elves here, Porridge given
<<else>>

    // --- Dialogue for after giving Elves Porridge here
    Oak: You return, customer!
    Ash: FRIEND. PORRIDGE FRIEND.
    Oak: We dream of that porridge, Ash and I do. Perhaps when you find another drink worthy of your <i>refined</i> taste buds, we can make one more trade?
    Ash: PORRIDGE~
    Ash: SOON.
    Oak: We didn't have a lot going for us while we worked in town--most working fair folk do not ask for payment, you know.
    Oak: We did it for the love of the craft.
    Ash: AND PORRIDGE.
    Oak: Yes, it was the porridge, that delicious oatmeal that made us stay in town for so long, but also spurred us to become craftsmen who are paid up front.
    Ash: WE DESERVES IT.
    Ash: WE MAKE BETTER DRINKS NOW. ASH IS HAPPY.
    Oak: A lesson, I think for an apprentice such as yourself.
    Ash: OATMEAL WISDOM.
    Oak: May you be inspired by your own porridge in time. Until then, you are always welcome at our door.

<<endif>>
===